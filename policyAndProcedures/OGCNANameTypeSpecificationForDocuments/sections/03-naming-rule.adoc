== Naming rule

=== OGC name schemes

The following URI schemes (<<rfc3986>>) are defined by OGC to provide persistent names for resources of interest in geographic information infrastructures. The generic syntax for OGC names is described in the OGC Naming Authority – Procedures policy (<<OGC_09-046r6>>).

Beginning 1 September 2024, the generic syntax for OGC http URIs of OGC Documents shall be

[%unnumbered%]
[source]
----
URI = "https://doi.org/10.62973/" DOISuffix
----

The legacy generic syntax for OGC http URIs is

[%unnumbered%]
[source]
----
URI = "http://www.opengis.net/" OGCResource "/" ResourceSpecificPath
----

The generic syntax for OGC URNs is (<<rfc5165>>)

[%unnumbered%]
[source]
----
URN = "urn:ogc:" OGCResource ":" ResourceSpecificString
----


NOTE: Beginning 1 September 2024, the external identifier of new OGC Documents shall be formed as an OGC DOI.

The following ABNF adapted from (<<rfc3986>>) provides some basic definitions required in the rest of this document.

[%unnumbered%]
[source]
----
segment = *pchar

segment-nc = *pchar-nc

segment-nz = 1*pchar

segment-nz-nc = 1*pchar-nc

pchar = unreserved / pct-encoded / sub-delims / ":" / "@"

pchar-nc = unreserved / pct-encoded / sub-delims / "@"

pct-encoded = "%" HEXDIG HEXDIG

unreserved = ALPHA / DIGIT / "-" / "." / "_" / "~"

reserved = gen-delims / sub-delims

gen-delims = ":" / "/" / "?" / "#" / "[" / "]" / "@"

sub-delims = "!" / "$" / "&" / "'" / "(" / ")"

/ "*" / "+" / "," / ";" / "="
----

=== Production rule for document names

An OGC name for a document or document element, based on the legacy generic syntax, shall be produced using the following rule:

[%unnumbered%]
[source]
----
OGCResource = "doc"

ResourceSpecificPath = doc-type "/" doc-identifier [ ( "#" / “/” ) doc-element]

ResourceSpecificString = doc-type ":" doc-identifierURN [":" doc-elementURN]

doc-type = segment-nz-nc; token from the register of OGC document types <1>

doc-identifier = doc-name ["/" doc-version]

doc-identifierURN = doc-name [":" doc-version]

doc-name = segment-nz-nc ; acronym or code for document topic

doc-version = 1*DIGIT "." 1*DIGIT ["." 1*DIGIT]

doc-element = doc-element-type ( "-" / "/") element-number

doc-elementURN = doc-element-type ":" element-number

doc-element-type = segment-nz-nc; token from the register of OGC document element types <2>

element-number = 1*DIGIT *("." 1*DIGIT)
----


`<1>` The register of OGC document types is at http://www.opengis.net/def/doc-type.

`<2>` The register of OGC document element types is at http://www.opengis.net/def/

NOTE: The document element (clause, table, figure, etc) is optional, If present, in the http URI form the element designator follows either a `/` or `#`, and sub-elements may be separated by either `/` or `-`. If the `#` form is used as a fragment separator resolution of the secondary resource is delegated to the http client. If the `#` form is used as a fragment separator, the `-` is recommended as the separator for sub-elements.
