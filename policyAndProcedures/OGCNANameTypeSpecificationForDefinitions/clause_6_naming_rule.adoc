== Naming Rule

This section describes the naming rule.

=== OGC name schemes

Two URI schemes [IETF RFC 3986] are defined by OGC to provide persistent names for resources of interest in geographic information infrastructures. The generic syntax for OGC names is described in [OGC Naming Authority – Procedures].

The generic syntax for OGC http URIs is

  URI = "http://www.opengis.net/" OGCResource "/" ResourceSpecificPath

The generic syntax for OGC URNs is [IETF RFC 5165]

  URN = "urn:ogc:" OGCResource ":" ResourceSpecificString

The following ABNF adapted from [IETF RFC 3986] provides some basic definitions required in the rest of this document.

  segment       = *pchar
  segment-nc    = *pchar-nc
  segment-nz    = 1*pchar
  segment-nz-nc = 1*pchar-nc
  pchar         = unreserved / pct-encoded / sub-delims / ":" / "@"
  pchar-nc      = unreserved / pct-encoded / sub-delims / "@"
  pct-encoded   = "%" HEXDIG HEXDIG
  unreserved    = ALPHA / DIGIT / "-" / "." / "_" / "~"
  reserved      = gen-delims / sub-delims
  gen-delims    = ":" / "/" / "?" / "#" / "[" / "]" / "@"
  sub-delims    = "!" / "$" / "&" / "'" / "(" / ")"
                  / "*" / "+" / "," / ";" / "="

=== Production rule for specification element names

The basic form for an OGC name that identifies a definition shall be produced using the following rule:

  OGCResource   = "def"
  ResourceSpecificPath = definition-type "/" authority "/" version "/" code
  ResourceSpecificString = definition-type ":" authority ":" versionURN ":" codeURN
  definition-type = segment-nz-nc ; a token from the register of OGC definition types1
  authority     = segment-nz-nc ; a token from the register of OGC authorities2
  version       = segment-nz-nc / "0" ; use 0 for un-versioned names
  code          = segment-nz-nc *( "/" segment-nz-nc )
  versionURN    = segment-nc ; this may be a zero-length string
  codeURN       = segment-nz-nc *( ":" segment-nz-nc )

"version" or "versionURN" is a required field. For un-versioned definitions:

* within the http URI form the version field shall be "0"
* within the URN form versionURN shall be a zero-length string—so an un-versioned definition can be detected by a pair of colons "::".

The actual code may be composed of a sequence of fields delimited by "/" in the http URI form, or ":" in the URN form.


Note that the register of definition types (http://www.opengis.net/register/ogc-na/def-type) was previously presented as Table 2 in the OGC Best Practice Definition identifier URNs in OGC namespace OGC 07-092r1.

Note that the register of authorities (http://www.opengis.net/register/ogc-na/authority) was previously presented as Table 1 in the OGC Best Practice Definition identifier URNs in OGC namespace OGC 07-092r1
